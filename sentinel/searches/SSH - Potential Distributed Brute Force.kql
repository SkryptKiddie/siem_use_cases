let threshold = 2; // define the threshold of hosts to be accessed before the analytic should fire
let failureToHostRation = 15; // define the minimum ratio of failures per host before the analytic should fire
Syslog
| where (Facility == "auth" and ProcessName == "sshd") and SyslogMessage startswith "Invalid user"
| parse kind=relaxed SyslogMessage with * "Invalid user " UserName " from " IpAddress " port " *
| summarize dcount(HostName), make_set(HostName), make_set(UserName), min(TimeGenerated), max(TimeGenerated), dcount(strcat(TimeGenerated, HostName, SyslogMessage)) by IpAddress
| extend TimeDiff = (max_TimeGenerated - min_TimeGenerated)
| project-rename StartTime=min_TimeGenerated, EndTime=max_TimeGenerated, HostList=set_HostName, UserList=set_UserName, HostCount=dcount_HostName, AttemptCount=dcount_
| where isnotempty(IpAddress) and (HostCount > threshold) and (AttemptCount > HostCount * failureToHostRation)